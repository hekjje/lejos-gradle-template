plugins {
    id 'java'
    id 'application'
}

version = projectVersion
mainClassName = projectMainClassName

compileJava {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
}

configurations {
    sshAntTask
    provided
}


sourceSets {
    main.compileClasspath += configurations.provided
    test.compileClasspath += configurations.provided
    test.runtimeClasspath += configurations.provided
}

repositories {
    mavenCentral()
    flatDir {
       dirs  System.getenv('EV3_HOME')+'/lib/ev3'
    }
}

dependencies {
    sshAntTask 'org.apache.ant:ant-jsch:1.9.4', 'com.jcraft:jsch:0.1.54'
    provided group: 'lejos', name: 'ev3classes', version: '1.0'

    testCompile group: 'junit', name: 'junit', version: '4.12'
}

jar {
    manifest {
        attributes 'Implementation-Title': projectTitle, 'Implementation-Version': version, 'Main-Class': mainClassName
    }
      // note this builds one fat jar and it is not recommended for production use - just for illustration purpose
	from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
}

task wrapper(type: Wrapper) {
  gradleVersion = '3.3'
  distributionUrl = "https://services.gradle.org/distributions/gradle-$gradleVersion-all.zip"
}

task copyDependencies(type: Copy) {
    description 'Copies the declared dependencies to the dependencies folder.'
    [ 'compile','runtime','testCompile', 'provided' ].each { mode ->
    copy {
        from configurations.getByName(mode).files
        into 'dependencies'
    }
  }
}

task deploy() {
    description 'Deploys the jar file to the EV3 brick';
    doLast {
        ant.taskdef(name: 'scp', classname: 'org.apache.tools.ant.taskdefs.optional.ssh.Scp', classpath: configurations.sshAntTask.asPath)
        ant.scp(todir: ev3Username+'@'+ev3Server+':/home/lejos/programs',
        password: ev3Password,verbose: 'true') {
            fileset(dir: './build/libs') {
                include(name: '**/*.jar')
            }
        }    
    }
}